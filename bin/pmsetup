#!/usr/bin/env perl
use strict;
use warnings;
use lib::xi;
use File::Basename;
use File::Path;
use File::Spec;
use YAML;
use Config;
use Text::Xslate;
use IO::Prompt::Simple qw(prompt);
use Software::License;
use Module::Build;
use Term::ANSIColor qw(colored);
my $path   = File::Spec->catfile($ENV{HOME}, "/.pmsetuprc");
my $config = eval { YAML::LoadFile($path) } || {};

my $save;
while (! $config->{author}) {
    $config->{author} = prompt 'Your name';
    $save++;
}

while (! $config->{email}) {
    $config->{email} = prompt 'Your email';
    $save++;
}

my $modname = shift @ARGV or die "Usage: pmsetup module\n";
   $modname =~ s/-/::/g;

write_plugin_files($modname, $config);

END {
    YAML::DumpFile($path, $config) if $save;
}

sub command {
    my @args = @_;
    print colored [qw/green/], '$ ', join(q{ }, @args), "\n";
    !system @args or die "command failed: $?";
}

sub write_plugin_files {
    my($module, $config) = @_;

    # $module = "Foo::Bar"
    # $dist   = "Foo-Bar"
    # $path   = "Foo/Bar.pm"
    my @pkg  = split /::/, $module;
    my $dist = join "-", @pkg;
    my $path = join("/", @pkg) . ".pm";

    my $dist_dir = sprintf 'p5-%s', $dist;

    mkdir $dist_dir, 0777;
    chdir $dist_dir;

    my @template = YAML::Load(join '', <DATA>);
    my $vars = {
        module    => $module,
        dist      => $dist,
        dist_dir  => $dist_dir,
        path      => $path,
        config    => $config,
        localtime => scalar localtime,
        year      => (localtime)[5] + 1900,
    };

    for my $tmpl (@template) {
        my $file = $tmpl->{file};
           $file =~ s/(\$\w+)/$1/eeg;
        write_file($file, $tmpl->{template}, $vars);
    }

    # symlink README.pod
    command 'ln', '-s', "lib/$path", 'README.pod';

    command 'git', 'init';
    command 'git', 'add', '.gitignore';

    opendir my $dh, '.' or die $!;
    while (my $name = readdir $dh) {
        next if $name =~ /^\.{1,2}$/ || $name eq '.git';
        command 'git', 'add', $name;
    }
    command 'git', 'commit', '-m', 'initial commit';

    my $builder = './Build';
    command $^X, 'Build.PL';
    command $^X, $builder;
    command $^X, $builder, 'manifest';
    command $^X, $builder, 'distmeta';
    command $^X, $builder, 'test';
    command $^X, $builder, 'realclean';

    print "\ndone.\n";
}

sub write_file {
    my($path, $template, $vars) = @_;

    if (-e $path) {
        my $ans = prompt "$path exists. Override?", { yn => 1, default => 'n' };
        return if $ans !~ /[Yy]/;
    }

    my $dir = File::Basename::dirname($path);
    unless (-e $dir) {
        warn "Creating directory $dir\n";
        File::Path::mkpath($dir, 1, 0777);
    }

    my $tx = Text::Xslate->new(syntax => 'TTerse', cache => 0);
    my $content = $tx->render_string($template, $vars);

    warn "Creating $path\n";
    open my $out, ">:raw", $path or die "$path: $!";
    print $out $content;
    close $out;
}

__DATA__
---
file: Build.PL
template: |
  use strict;
  use warnings;
  use Module::Build;

  my $build = Module::Build->new(
      license        => 'perl',
      dynamic_config => 0,
      dist_abstract  => 'XXX',

      build_requires     => {
          'Test::More'     => '0.98',
          'Test::Requires' => '0.06',
      },
      configure_requires => { 'Module::Build' => '0.38' },
      requires           => {
          perl => '5.008001',
      },

      no_index    => { 'directory' => [ 'inc' ] },
      name        => '[% dist %]',
      module_name => '[% module %]',

      # script_files => [''],

      test_files           => (-d '.git' || $ENV{RELEASE_TESTING}) ? 't/ xt/' : 't/',
      recursive_test_files => 1,

      create_readme  => 1,
      create_license => 1,
  );

  $build->create_build_script();
---
file: t/00_compile.t
template: |
  use strict;
  use warnings;
  use Test::More tests => 1;

  BEGIN { use_ok '[% module %]' }
---
file: xt/01_podspell.t
template: |
  use strict;
  use warnings;
  use Test::More;
  use Test::Requires 'Test::Spelling';
  use Config;
  use File::Spec;
  use ExtUtils::MakeMaker;

  my %cmd_map = (
      spell    => 'spell',
      aspell   => 'aspell list -l en',
      ispell   => 'ispell -l',
      hunspell => 'hunspell -d en_US -l',
  );

  my $spell_cmd;
  for my $dir ((split /$Config::Config{path_sep}/, $ENV{PATH}), '.') {
      next if $dir eq '';
      ($spell_cmd) = map { $cmd_map{$_} } grep {
          my $abs = File::Spec->catfile($dir, $_);
          -x $abs or MM->maybe_command($abs);
      } keys %cmd_map;
      last if $spell_cmd;
  }
  $spell_cmd = $ENV{SPELL_CMD} if $ENV{SPELL_CMD};
  plan skip_all => "spell command are not available." unless $spell_cmd;
  add_stopwords(map { split /[\s\:\-]/ } <DATA>);
  set_spell_cmd($spell_cmd);
  $ENV{LANG} = 'C';
  all_pod_files_spelling_ok('lib');

  __DATA__
  [% module %]

  # personal section
  [% config.author %]
  [% config.email %]
---
file: xt/02_pod.t
template: |
  use strict;
  use warnings;
  use Test::More;
  use Test::Requires { 'Test::Pod' => 1.00 };

  all_pod_files_ok();
---
file: xt/03_pod-coverage.t
template: |
  use strict;
  use warnings;
  use Test::More;
  use Test::Requires { 'Test::Pod::Coverage' => 1.04 };

  unless ($ENV{TEST_POD_COVERAGE}) {
      plan skip_all => "\$ENV{TEST_POD_COVERAGE} is not set.";
      exit;
  }

  all_pod_coverage_ok({also_private => [qw(unimport BUILD DEMOLISH)]});
---
file: xt/04_perlcritic.t
template: |
  use strict;
  use warnings;
  use Test::More;
  use Test::Requires { 'Test::Perl::Critic' => 1.02 };

  Test::Perl::Critic->import(-profile => 'xt/perlcriticrc');

  all_critic_ok('lib');
---
file: xt/05_script-shebang.t
template: |
  use strict;
  use warnings;
  use Test::More;
  use Test::Requires 'Test::Script::Shebang';
  use File::Find qw/find/;

  my @files;
  for my $dir (qw/bin script/) {
      next unless -d $dir;
      find {
          no_chdir => 1,
          wanted   => sub { push @files, $_ if -f },
      }, $dir;
  }
  plan skip_all => 'script not found' unless @files;

  check_shebang(@files);

  done_testing;
---
file: xt/perlcriticrc
template: |
  [TestingAndDebugging::ProhibitNoStrict]
  allow=refs
  [-Subroutines::ProhibitSubroutinePrototypes]
---
file: Changes
template: |
  Revision history for Perl extension [% module %]

  0.01  [% localtime %]
      - original version
---
file: lib/$path
template: |
  package [% module %];

  use strict;
  use warnings;
  use 5.008_001;
  our $VERSION = '0.01';

  1;
  __END__

  =encoding utf-8

  =for stopwords

  =head1 NAME

  [% module %] -

  =head1 SYNOPSIS

    use [% module %];

  =head1 DESCRIPTION

  [% module %] is

  =head1 AUTHOR

  [% config.author %] E<lt>[% config.email %]E<gt>

  =head1 COPYRIGHT

  Copyright [% year %] - [% config.author %]

  =head1 LICENSE

  This library is free software; you can redistribute it and/or modify
  it under the same terms as Perl itself.

  =head1 SEE ALSO

  =cut
---
file: MANIFEST.SKIP
template: |
  \bRCS\b
  \bCVS\b
  \.svn/
  \.git/
  ^MANIFEST\.
  ^Makefile$
  ~$
  \.old$
  ^blib/
  ^pm_to_blib
  ^MakeMaker-\d
  \.gz$
  \.shipit
  \.gitignore
  \ppport.h
  ^MYMETA\.

  # for Module::Build
  \bBuild$
  \b_build/
  \bBuild.bat$
  \bBuild.COM$
  \bBUILD.COM$
  \bbuild.com$
---
file: .shipit
template: |
  steps = FindVersion, ChangeVersion, CheckChangeLog, DistTest, Commit, Tag, MakeDist, UploadCPAN
  git.push_to = origin
---
file: .gitignore
template: |
  cover_db
  MYMETA.*
  META.yml
  META.json
  Makefile
  blib
  inc
  pm_to_blib
  MANIFEST
  MANIFEST.bak
  Makefile.old
  nytprof*
  ppport.h
  xs/*c
  xs/*o
  xs/*obj
  *.bs
  *.def
  *.old
  dll*
  *~
  README
  LICENSE
---
file: .proverc
template: |
  --exec "perl -Ilib -MTest::Flatten -MTest::Name::FromLine"
  --timer
  --merge
  --trap
  --color
  -w
file: .travis.yml
template: |
  language: perl
  perl:
    - 5.16
    - 5.14
    - 5.12
