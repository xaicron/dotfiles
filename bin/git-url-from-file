#!/usr/bin/env perl

use strict;
use warnings;
use feature 'say';
use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat bundling);
use Pod::Usage qw(pod2usage);
use Cwd qw(realpath);

my $opts = {};
GetOptions(
    'h|help' => sub { pod2usage(0) },
    'o|open' => \$opts->{open},
) or pod2usage(1);

my $file = shift || pod2usage(2);
$file = realpath $file;
my $line_start = shift;
my $line_end   = shift;

chomp(my $remote = `git gh-url`);
exit $? if $?;

chomp(my $branch = `git symbolic-ref HEAD`);
exit $? if $?;
$branch =~ s!^refs/heads/!!;

chomp(my $git_top = `git rev-parse --show-toplevel`);
$git_top = quotemeta $git_top;
$file =~ s|^$git_top/||;

my $url = sprintf '%s/tree/%s/%s', $remote, $branch, $file;
$url .= "#L$line_start" if $line_start;
$url .= "..L$line_end"  if $line_end;

if ($opts->{open}) {
    system 'open', $url;
}
else {
    say $url;
}

__END__

=head1 NAME

git-url-from-file - display github url filename

=head1 SYNOPSIS

    git url-from_file [option] <file> [line-start, line-end]

=head1 OPTIONS

    -o, --open    Open result of the URL if your system is OS X.
    -h, --help    Show this message.

